#
# A two-line, Powerline-inspired theme that displays contextual information.
#
# This theme requires a patched Powerline font, get them from
# https://github.com/Lokaltog/powerline-fonts.
#
# Authors:
#   Isaac Wolkerstorfer <i@agnoster.net>
#   Jeff Sandberg <paradox460@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Forked from paradox
# Modified by: Kyle Galloway

# Load dependencies.
pmodload 'helper'

# Define variables.
_prompt_kgalloway_current_bg='NONE'
_prompt_kgalloway_segment_separator=''

function prompt_kgalloway_start_segment {
  local bg fg
  [[ -n "$1" ]] && bg="%K{$1}" || bg="%k"
  [[ -n "$2" ]] && fg="%F{$2}" || fg="%f"
  if [[ "$_prompt_kgalloway_current_bg" != 'NONE' && "$1" != "$_prompt_kgalloway_current_bg" ]]; then
    print -n " $bg%F{$_prompt_kgalloway_current_bg}$_prompt_kgalloway_segment_separator$fg "
  else
    print -n "$bg$fg "
  fi
  _prompt_kgalloway_current_bg="$1"
  [[ -n "$3" ]] && print -n "$3"
}

function prompt_kgalloway_end_segment {
  if [[ -n "$_prompt_kgalloway_current_bg" ]]; then
    print -n " %k%F{$_prompt_kgalloway_current_bg}$_prompt_kgalloway_segment_separator"
  else
    print -n "%k"
  fi
  print -n "%f"
  _prompt_kgalloway_current_bg=''
}

function prompt_kgalloway_build_prompt {
  # This gives information on return values (%?:), history? (%!), and background jobs (%1j)
  # prompt_kgalloway_start_segment black default '%(?::%F{red}✘ )%(!:%F{yellow}⚡ :)%(1j:%F{cyan}⚙ :)%f'

  # History line number
  prompt_kgalloway_start_segment blue black '%!'

  # User@Machine
  # prompt_kgalloway_start_segment cyan black '%n@%m'

  # No user@machine
  prompt_kgalloway_start_segment magenta black '$_prompt_kgalloway_pwd'

  if [[ -n "$git_info" ]]; then
    prompt_kgalloway_start_segment green black '${(e)git_info[ref]}${(e)git_info[status]}'
  fi

  prompt_kgalloway_end_segment
}

function prompt_kgalloway_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_kgalloway_pwd="$MATCH"
    unset MATCH
  else
    _prompt_kgalloway_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_kgalloway_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_kgalloway_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Calculate and print the elapsed time.

# I removed this so the elapsed time would not show.
#  prompt_kgalloway_print_elapsed_time
}

function prompt_kgalloway_preexec {
}

function prompt_kgalloway_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook preexec prompt_kgalloway_preexec
  add-zsh-hook precmd prompt_kgalloway_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{blue}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ' ⁝ %s'
  zstyle ':prezto:module:git:info:added' format ' ✚'
  zstyle ':prezto:module:git:info:ahead' format ' ⬆'
  zstyle ':prezto:module:git:info:behind' format ' ⬇'
  zstyle ':prezto:module:git:info:branch' format ' %b'
  zstyle ':prezto:module:git:info:commit' format '➦ %.7c'
  zstyle ':prezto:module:git:info:deleted' format ' ✖'
  zstyle ':prezto:module:git:info:dirty' format ' ⁝'
  zstyle ':prezto:module:git:info:modified' format ' ✱'
  zstyle ':prezto:module:git:info:position' format '%p'
  zstyle ':prezto:module:git:info:renamed' format ' ➙'
  zstyle ':prezto:module:git:info:stashed' format ' S'
  zstyle ':prezto:module:git:info:unmerged' format ' ═'
  zstyle ':prezto:module:git:info:untracked' format ' ?'
  zstyle ':prezto:module:git:info:keys' format \
    'ref' '$(coalesce "%b" "%p" "%c")' \
    'status' '%s%D%A%B%S%a%d%m%r%U%u'

  # Define prompts.

  # Left prompt
  PROMPT='${(e)$(prompt_kgalloway_build_prompt)} ${editor_info[keymap]} '
  # Right prompt
  RPROMPT='%F{blue}[%F{green}%t %F{blue}]%f'
  # Continuation prompt.
  export PROMPT2='(%_) ${editor_info[keymap]}'
  # Selection prompt.
  export PROMPT3='
(?) ${editor_info[keymap]}'
  # Execution trace prompt.
  export PROMPT4='${yellow}+${blue}%N ${green}@${magenta}%i ${reset_color}'
  # Autocorrection prompt
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

  # Backup root prompt for Bash.
  export SUDO_PS1="\[\e[31;1;46m\][\u] \w \$\[\e[0m\] "
}

prompt_kgalloway_setup "$@"
